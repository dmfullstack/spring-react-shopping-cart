buildscript {
  ext {
    springBootVersion = '1.2.3.RELEASE'
  }
  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("io.spring.gradle:dependency-management-plugin:0.4.1.RELEASE")
    classpath("net.eikehirsch.react:gradle-react-plugin:0.3.1")
  }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'net.eikehirsch.react'
apply plugin: 'eclipse'

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group == 'org.springframework') {
        details.useVersion '4.2.0.BUILD-SNAPSHOT'
      }
    }
  }
}

jar {
  baseName = 'spring-react-isomorphic'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
  maven { url 'http://repo.spring.io/snapshot' }
}


dependencies {
  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.webjars:react:0.13.1")
  testCompile("org.springframework.boot:spring-boot-starter-test")
}

jsx {
  sourcesDir = 'src/main/resources/static/jsx'
  destDir    = 'src/main/resources/static/output'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}

processResources.dependsOn('jsx')
	
